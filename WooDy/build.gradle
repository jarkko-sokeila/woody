plugins {
    id 'java'
    id 'jacoco'
    id "com.palantir.git-version" version "0.12.3"
    id "org.springframework.boot" version "2.4.2"
    id "io.spring.dependency-management" version "1.0.10.RELEASE"
}

repositories {
    mavenCentral()
}

def details = versionDetails()
version = details.lastTag

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'com.h2database:h2'
    
    implementation 'org.flywaydb:flyway-core'
    
    implementation 'com.rometools:rome:1.12.0'
    implementation 'com.google.guava:guava:30.1-jre'
    implementation 'org.apache.commons:commons-lang3:3.10'

    // Test
    testImplementation('org.springframework.boot:spring-boot-starter-test:2.3.1.RELEASE') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

bootJar {
    dependsOn ':frontend:buildFrontend'
    from('../frontend/build') {
        into 'static'
    }
}

test {
    useJUnitPlatform()
}

test.finalizedBy jacocoTestReport

jacoco {
    toolVersion = "0.8.6"
}

jacocoTestReport {
    reports {
        html.enabled true
        xml.enabled true
        csv.enabled false
    }
}